plugins {
    id "java"
    id 'idea'
    id "org.springframework.boot" version "2.2.6.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "org.asciidoctor.convert" version "2.4.0"
    id "net.ltgt.apt" version "0.20"
}

group = "random"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "12"

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    set("snippetsDir", file("build/generated-snippets"))
    mapstructVersion = "1.3.1.Final"
    jupiterVersion = "5.6.2"
    jaxbVersion = "2.3.0"
}

dependencies {
    implementation(
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.springframework.boot:spring-boot-starter-web",
            "org.mapstruct:mapstruct:$mapstructVersion",
            "javax.xml.bind:jaxb-api:$jaxbVersion",
            "com.sun.xml.bind:jaxb-core:$jaxbVersion",
            "com.sun.xml.bind:jaxb-impl:$jaxbVersion"
    )
    compileOnly "org.projectlombok:lombok"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly "com.h2database:h2"
    annotationProcessor(
            "org.projectlombok:lombok",
            "org.mapstruct:mapstruct-processor:$mapstructVersion"
    )
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
    testImplementation(
            "org.mockito:mockito-junit-jupiter:3.3.3",
            "org.springframework.restdocs:spring-restdocs-mockmvc",
    )
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    testRuntime "org.junit.jupiter:junit-jupiter:$jupiterVersion"
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    println("asciidoctor task")
    dependsOn test
    sourceDir file('src/main/asciidoc')
    outputDir "build/asciidoc"
    inputs.dir snippetsDir
    backends = ['html5']
    options doctype: 'book'

    attributes = [
            'source-highlighter': 'highlightjs',
            'imagesdir'         : './images',
            'toc'               : 'left',
            'toclevels'         : 3,
            'numbered'          : '',
            'icons'             : 'font',
            'setanchors'        : '',
            'idprefix'          : '',
            'idseparator'       : '-',
            'docinfo1'          : '',
            'safe-mode-unsafe'  : '',
            'allow-uri-read'    : '',
            'snippets'          : snippetsDir,
            linkattrs           : true,
            encoding            : 'utf-8'
    ]
    sources {
        include 'index.adoc' //, 'etc/internal.adoc'
    }
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
